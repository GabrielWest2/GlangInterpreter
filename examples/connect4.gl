var board = [
    [0, 0, 0, 0, 0, 0, 0],
    [0, 0, 0, 0, 0, 0, 0],
    [0, 0, 0, 0, 0, 0, 0],
    [0, 0, 0, 0, 0, 0, 0],
    [0, 0, 0, 0, 0, 0, 0],
    [0, 0, 0, 0, 0, 0, 0]
];

def getPlayerChar(u) {
    switch(u){
        case 1 return "X";
        case 2 return "O";
        case default return " ";
    }
}


def printBoard(){
    println("");
    for(var i = 0; i < 7; i++){
        print("[ " + i +" ]");
    }
    println("\n===================================");

    for(var c = 5; c >= 0; c--){
        print("| ");
        for(var r = 0; r < 7; r++){
            print(getPlayerChar(board[c][r]));
            print(r == 6 ? " |" : " or ");
        }
        println("");
    }
}

def checkPlayerWin(player){
        //check horizontal matches
        for(var y = 0; y < 6; y++){
            for(var xstart = 0; xstart < 4; xstart ++){
                var matches = 0;
                for(var x = 0; x < 4; x++){
                   if(board[y][xstart + x] == player){
                       matches ++;
                   }
                }
                if(matches == 4){
                    println("Connect 4 in row from ["+xstart + ", " + y + "] to ["+(xstart + 3) + ", " + y + "]");
                    return true;
                }
            }
        }

        // Check vertical connections
        for(var x = 0; x < 7; x++){
            for(var ystart = 0; ystart < 3; ystart ++){
                var matches = 0;
                for(var y = 0; y < 4; y++){
                   if(board[ystart + y][x] == player){
                       matches ++;
                   }
                }
                if(matches == 4){
                    println("Connect 4 in column from ["+x + ", " + ystart + "] to [" + x + ", " + (ystart + 3) + "]");
                    return true;
                }
            }
        }


        // Check diagonal of increasing slope
        for(var xstart = 0; xstart < 4; xstart++){
            for(var ystart = 0; ystart < 3; ystart++){
                var matches = 0;
                for(var i = 0; i < 4; i++){
                    if(board[ystart + i][xstart + i] == player){
                        matches ++;
                    }
                }
                if(matches == 4){
                    println("Connect 4 in diagonal from ["+xstart + ", " + ystart + "] to [" + (xstart + 3) + ", " + (ystart + 3) + "]");
                    return true;
                }
            }
        }

        // Check diagonal of decreasing slope
        for(var xstart = 0; xstart < 4; xstart++){
            for(var ystart = 0; ystart < 3; ystart++){
                var matches = 0;
                for(var i = 0; i < 4; i++){
                    if(board[ystart + 3 - i][xstart + i] == player){
                        matches ++;
                    }
                }
                if(matches == 4){
                    println("Connect 4 in diagonal from ["+xstart + ", " + (ystart + 3) + "] to [" + (xstart + 3) + ", " + (ystart) + "]");
                    return true;
                }
            }
        }

    return false;
}

def dropPiece(team, col){
    if(col < 0 or col > 6) return false;
    if(board[5][col] != 0) return false;

    for(var i = 0; i < 6; i ++){
        if(board[i][col] == 0){
            board[i][col] = team;
            return true;
        }
    }
    return false; // doesn't reach here
}


def isGameOver(){
    if(checkPlayerWin(1)) {
        println("Player 1 won!");
        return true;
    }
    if(checkPlayerWin(2)) {
        println("Player 1 won!");
        return true;
    }

     // Check for full board
    if(board[5][0] != 0 and board[5][1] != 0 and board[5][2] != 0 and board[5][3] != 0
                      and board[5][4] != 0 and board[5][5] != 0 and board[5][6] != 0){
        println("Tie game!");
        return true;
    }
    return false;
}


var team = 1;
while(!isGameOver()){
    printBoard();
    while(!dropPiece(team, scanNum())){
        println("You can't drop here!");
    }
    printBoard();
    team = team == 1 ? 2 : 1;
}